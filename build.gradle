
plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "io.freefair.lombok" version "6.3.0"
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

group "org.polypheny"
description = "Polypheny Query-to-File allows to mount a table or the result of a query as file system."

def versionMajor = 0
def versionMinor = 9
def versionQualifier = "-SNAPSHOT"
version = versionMajor + "." + versionMinor + versionQualifier

mainClassName = 'org.polypheny.qtf.Main'

applicationDefaultJvmArgs = [
    '-Dfile.encoding=UTF-8'
]

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32' // License: MIT
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.15.0' // License: Apache 2.0
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.15.0' // License: Apache 2.0

    implementation group: 'com.github.serceman', name: 'jnr-fuse', version: '0.5.5' // License: MIT
    implementation group: "org.apache.commons", name: "commons-lang3", version: '3.11' // License: Apache 2.0
    implementation group: "com.google.code.gson", name: "gson", version: '2.8.6' // License: Apache 2.0

    implementation group: 'com.konghq', name: 'unirest-java', version: '3.11.09' // License: MIT
    implementation group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.1' // License: MIT

    runtimeOnly group: 'org.openjfx', name: 'javafx-graphics', version: '15.0.1', classifier: 'win' // License: GPL with classpath exception
    runtimeOnly group: 'org.openjfx', name: 'javafx-graphics', version: '15.0.1', classifier: 'mac' // License: GPL with classpath exception
    runtimeOnly group: 'org.openjfx', name: 'javafx-graphics', version: '15.0.1', classifier: 'linux' // License: GPL with classpath exception
}

test {
    useJUnitPlatform()
}

javafx {
    version = '15.0.1'
    modules = [ 'javafx.controls', 'javafx.fxml']
}

/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project (polypheny.org)"
        attributes "Version": project.version
        attributes "Main-Class": mainClassName
        attributes "Multi-Release": "true"
        attributes "Implementation-Version": project.version
    }
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}
shadowJar {
    zip64 true
    classifier ""
}
assemble.dependsOn shadowJar
artifacts {
    //archives jar // regular jar containing only the compiled source
    archives shadowJar // fat jar which additionally contains all dependencies
    archives sourcesJar // jar file containing the java doc files
    archives javadocJar // jar file containing the source files
}
